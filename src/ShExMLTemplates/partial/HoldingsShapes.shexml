#########################
### Documentary Units ###
#########################

# Shape for RecordSet
ehri:RecordSet ehri_units:[holding.holding_id IF validators.isRecordSet(holding.itemCount)] { # Sometimes the level is not provided so we determine the type based on our own context
	a ehri:RecordSet ;
	rico:hasOrHadSomeMembersWithLanguage @ehri:Language ;
}

# Shape for RecordSet
ehri:Record ehri_units:[holding.holding_id IF validators.isRecord(holding.itemCount)] { # Sometimes the level is not provided so we determine the type based on our own context
	a ehri:Record ;
	rico:hasOrHadLanguage @ehri:Language ;
}

ehri:Holding ehri_units:[holding.holding_id] { # common shape
	# a ehri:[transformers.recordOrRecordSet(holding.descriptions.levelOfDescription, holding.itemCount)] ; # This allows for more fine-grained control of the type if needed
	rico:hasRecordSetType ric_rst:[transformers.getRecordSetType(holding.descriptions.levelOfDescription) IF validators.isRecordSetTypeDefinedInRiC(holding.descriptions.levelOfDescription)] ;
	rico:hasRecordSetType ehri_rst:[transformers.getEHRIDefinedRecordSetType(holding.descriptions.levelOfDescription) IF validators.isRecordSetTypeDefinedInEHRI(holding.descriptions.levelOfDescription)] ;
	rico:title [holding.descriptions.name] @[holding.descriptions.languageCode] ;
	#rico:hasOrHadTitle @ehri:HoldingParralelFormOfName ;
	rico:scopeAndContent [holding.descriptions.scopeAndContent] @[holding.descriptions.languageCode] ;
	rico:recordResourceExtent [holding.descriptions.extentAndMedium] @[holding.descriptions.languageCode] ;
	rico:recordResourceStructure [holding.descriptions.systemOfArrangement] @[holding.descriptions.languageCode] ;
	rico:date [holding.descriptions.datesOfDescription] ;
	rico:date [holding.descriptions.unitDates] ;
	rico:hasBeginningDate @ehri:HoldingBeginningDate ;
	rico:hasEndDate @ehri:HoldingEndDate ;
	rico:isOrWasIncludedIn ehri_units:[holding.parentId] ;
	rico:resultsOrResultedFrom @ehri:Acquisition ;
	rico:identifier [holding.descriptions.identifier] ;
	rico:history [holding.descriptions.biographicalHistory] @[holding.descriptions.languageCode] ;
	rico:history [holding.descriptions.archivalHistory] @[holding.descriptions.languageCode] ;
	ehri:appraisal [holding.descriptions.appraisal] @[holding.descriptions.languageCode] ;
	rico:accruals [holding.descriptions.accruals] @[holding.descriptions.languageCode] ;
	rico:conditionsOfAccess [holding.descriptions.conditionsOfAccess] @[holding.descriptions.languageCode] ; 
	rico:conditionsOfUse [holding.descriptions.conditionsOfUse] @[holding.descriptions.languageCode] ; 
	rico:conditionsOfUse [holding.descriptions.conditionsOfReproduction] @[holding.descriptions.languageCode] ;
	ehri:scriptOfMaterials @ehri:MaterialScript ;
	ehri:physicalCharacterisiticsAndTechnicalRequirements [holding.descriptions.physicalCharacteristics] @[holding.descriptions.languageCode] ; 
	ehri:otherFindingAids [holding.descriptions.findingAids] @[holding.descriptions.languageCode] ; 
	ehri:locationOfOriginals [holding.descriptions.locationOfOriginals] @[holding.descriptions.languageCode] ; 
	ehri:locationOfCopies [holding.descriptions.locationOfCopies] @[holding.descriptions.languageCode] ; 
	ehri:wasUsedAsSourceIn [holding.descriptions.publicationNote] @[holding.descriptions.languageCode] ; 
	ehri:sources [holding.descriptions.sources] @[holding.descriptions.languageCode] ; 
	ehri:notes [holding.descriptions.notes] @[holding.descriptions.languageCode] ; 
	ehri:archivistNote [holding.descriptions.archivistNote] @[holding.descriptions.languageCode] ; 
	ehri:relatedMaterial [holding.descriptions.relatedUnitsOfDescription] @[holding.descriptions.languageCode] ; 
	ehri:separatedMaterial [holding.descriptions.separatedUnitsOfDescription] @[holding.descriptions.languageCode] ; 
	rico:hasOrHadHolder ehri_institutions:[holding.repositoryId] ;
	# rdfs:seeAlso [] Has to be implemented in the API
	#rico:hasOrHadIdentifier @ehri:AlternateIds ;
}

# ehri:HoldingParralelFormOfName ehri_parallel_name:[holding.descriptions.parallel_names.holding_id] { # better id is needed and for now it only takes into account that one parallel name can exist
# 	a rico:Title ;
# 	rico:name [holding.descriptions.parallel_names.name] @[holding.descriptions.parallel_names.lang_code] ;
# }

ehri:HoldingBeginningDate _:[holding.descriptions.dates.startDate] { 
	a rico:Date ;
	rico:normalizedDateValue [holding.descriptions.dates.startDate] ;
	rico:isBeginningDateOf ehri_units:[holding.descriptions.dates.holding_id] ;
}

ehri:HoldingEndDate _:[holding.descriptions.dates.endDate] { 
	a rico:Date ;
	rico:normalizedDateValue [holding.descriptions.dates.endDate] ;
	rico:isBeginningDateOf ehri_units:[holding.descriptions.dates.holding_id] ;
}

ehri:ArchiveComponent ehri_units:[holding.parentId] {
	rico:includesOrIncluded ehri_units:[holding.holding_id] ;
}

ehri:Language ehri_languages:[holding.descriptions.languageOfMaterial.lang] {
   	a rico:Language ;
   	rico:name [holding.descriptions.languageOfMaterial.lang] ;
}

ehri:Acquisition ehri_units:[transformers.combinedPathHoldingAcquisition(holding.holding_id) IF validators.nonEmpty(holding.descriptions.acquisition)] {
	a rico:Activity ;
	rico:hasActivityType ehri:AcquisitionType ;
	rico:generalDescription [holding.descriptions.acquisition] @[holding.descriptions.languageCode] ;
}

ehri:MaterialScript ehri_material_script:[holding.descriptions.scriptOfMaterial.script] {
	a ehri:MaterialScript ;
	rico:name [holding.descriptions.scriptOfMaterial.script] ;
}

# ehri:AlternateIds ehri_alternate_ids:[alternateIdComposedId] {
# 	a rico:Identifier ;
# 	rico:textualValue [holding.alternate_ids.holding_id] ;
# 	rico:isOrWasIdentifierOf ehri_units:[holding.alternate_ids.holding_id] ;
# }

##################
### Copy links ###
##################

# In these links it is not possible to put the description unless we reify the data model, TBD.

# Subject documentary unit
ehri:HoldingLocationOfOriginals ehri_units:[holding.holding_id IF validators.isDocumentaryUnit(holding.links.loc_org.targets.subject_id, holding.links.loc_org.targets.target_id, holding.links.loc_org.targets.type)] {
    # The object is a documentary unit
    ehri:isCopyOf ehri_units:[holding.links.loc_org.targets.target_id IF validators.isObjectDocumentaryUnit(holding.links.loc_org.targets.subject_id, holding.links.loc_org.targets.target_id, holding.links.loc_org.targets.type)] ;
    # The object is an institution
    ehri:isCopyOf ehri_institutions:[holding.links.loc_org.targets.target_id IF validators.isObjectInstitution(holding.links.loc_org.targets.subject_id, holding.links.loc_org.targets.target_id, holding.links.loc_org.targets.type)] ;
}

ehri:HoldingLocationOfCopies ehri_units:[holding.holding_id IF validators.isDocumentaryUnit(holding.links.loc_cop.targets.subject_id, holding.links.loc_cop.targets.target_id, holding.links.loc_cop.targets.type)] {
    # The object is a documentary unit
    ehri:hasCopy ehri_units:[holding.links.loc_cop.targets.target_id IF validators.isObjectDocumentaryUnit(holding.links.loc_cop.targets.subject_id, holding.links.loc_cop.targets.target_id, holding.links.loc_cop.targets.type)] ;
    # The object is an institution
    ehri:hasCopy ehri_institutions:[holding.links.loc_cop.targets.target_id IF validators.isObjectInstitution(holding.links.loc_cop.targets.subject_id, holding.links.loc_cop.targets.target_id, holding.links.loc_cop.targets.type)] ;
}

ehri:HoldingLocationOfOriginalsInverse ehri_units:[holding.links.loc_org.targets.target_id] {
    ehri:hasCopy ehri_units:[holding.links.loc_org.targets.subject_id IF validators.isObjectDocumentaryUnit(holding.links.loc_org.targets.subject_id, holding.links.loc_org.targets.target_id, holding.links.loc_org.targets.type)] ;
}

ehri:HoldingLocationOfOriginalsInverseInstitutionSubject ehri_institutions:[holding.links.loc_org.targets.target_id IF validators.isObjectInstitution(holding.links.loc_org.targets.subject_id, holding.links.loc_org.targets.target_id, holding.links.loc_org.targets.type)] { 
    ehri:hasCopy ehri_units:[holding.links.loc_org.targets.subject_id] ;
}    

ehri:HoldingLocationOfCopiesInverse ehri_units:[holding.links.loc_cop.targets.target_id] {
    ehri:isCopyOf ehri_units:[holding.links.loc_cop.targets.subject_id IF validators.isObjectDocumentaryUnit(holding.links.loc_cop.targets.subject_id, holding.links.loc_cop.targets.target_id, holding.links.loc_cop.targets.type)] ;
}

ehri:HoldingLocationOfCopiesInverseInstitutionSubject ehri_institutions:[holding.links.loc_cop.targets.target_id IF validators.isObjectInstitution(holding.links.loc_cop.targets.subject_id, holding.links.loc_cop.targets.target_id, holding.links.loc_cop.targets.type)] { 
    ehri:isCopyOf ehri_units:[holding.links.loc_cop.targets.subject_id] ;
}

#####################
### Creator links ###
#####################

ehri:HoldingCreator ehri_units:[holding.links.creators.unit_id] {
    rico:hasCreator ehri_cb:[transformers.removeCBPrefix(holding.links.creators.cb_id) IF validators.hasCBPrefix(holding.links.creators.cb_id)] ; # Apparently only CBs are returned in creator links, but this could be expanded if need be.
}

ehri:HoldingCreatorInverse ehri_cb:[transformers.removeCBPrefix(holding.links.creators.cb_id) IF validators.hasCBPrefix(holding.links.creators.cb_id)] {
    rico:isCreatorOf ehri_units:[holding.links.creators.unit_id] ; # Apparently only CBs are returned in creator links, but this could be expanded if need be.
}